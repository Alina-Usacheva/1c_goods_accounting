
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТовараТовары.Сумма) КАК Сумма,
		|	СУММА(ПродажаТовараТовары.Количество) КАК Количество,
		|	ПродажаТовараТовары.Ссылка.Склад КАК Склад
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		|ГДЕ
		|	ПродажаТовараТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТовараТовары.Номенклатура,
		|	ПродажаТовараТовары.Ссылка.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Сумма КАК Сумма,
		|	втТовары.Количество КАК Количество,
		|	втТовары.Склад КАК Склад
		|ИЗ
		|	втТовары КАК втТовары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиТоваров.Очистить();
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаТовары.Номенклатура;
		Движение.Склад = ВыборкаТовары.Склад;
		Движение.Количество = ВыборкаТовары.Количество;
		Движение.Сумма = ВыборкаТовары.Сумма;		
	КонецЦикла;

	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	-ОстаткиТоваровОстатки.КоличествоОстаток КАК Дефицит
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					втТовары.Номенклатура КАК Номенклатура,
		|					втТовары.Склад КАК Склад
		|				ИЗ
		|					втТовары КАК втТовары)) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        ВыборкаОшибки = РезультатЗапроса.Выбрать();
        Пока ВыборкаОшибки.Следующий() Цикл
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Товара " + ВыборкаОшибки.Номенклатура + " недостаточно в количестве " + ВыборкаОшибки.Дефицит + " шт.";
            Сообщение.Сообщить();
        КонецЦикла;
    КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТовара") Тогда
		Склад = ДанныеЗаполнения.Склад;
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
			НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
			НоваяСтрока.Цена = ТекСтрокаТовары.Цена;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
